// *** UI functions for adding action feedback slider control ***

INCLUDE ~%MOD_FOLDER%/lib/functions.tph~

/**
 * This code snippet adds a new action feedback slider control.
 * The following variables are required to be set beforehand:
 * INT_VAR Label text position: ctl_label_x, ctl_label_y, ctl_label_w, ctl_label_h
 * INT_VAR Slider position: ctl_slider_x, ctl_slider_y, ctl_slider_w, ctl_slider_h
 * INT_VAR Slider info position: ctl_info_x, ctl_info_y, ctl_info_w, ctl_info_h
 * INT_VAR Slider padding (only needed for IWDEE): ctl_pad_x, ctl_pad_y, ctl_pad_w, ctl_pad_h
 * INT_VAR Control ID: ctl_id
 * INT_VAR Number of slider items: ctl_num_settings
 * STR_VAR LUA label text variable: ctl_label
 * STR_VAR LUA description variable: ctl_description
 */
OUTER_TEXT_SPRINT codeActionFeedbackSlider ~%TAB%
%TAB%text
%TAB%{
%TAB%%TAB%area %ctl_label_x% %ctl_label_y% %ctl_label_w% %ctl_label_h%
%TAB%%TAB%text "%ctl_label%"
%TAB%%TAB%text style "label"
%TAB%%TAB%text align right center
%TAB%%TAB%text upper
%TAB%%TAB%text highlight lua "getSelected(selectedSL, %ctl_id%)"
%TAB%%TAB%action
%TAB%%TAB%"
%TAB%%TAB%%TAB%helpString = '%ctl_description%'
%TAB%%TAB%%TAB%selectedOpt = 0
%TAB%%TAB%%TAB%selectedSL = %ctl_id%
%TAB%%TAB%"
%TAB%}
%TAB%slider
%TAB%{
%TAB%%TAB%area %ctl_slider_x% %ctl_slider_y% %ctl_slider_w% %ctl_slider_h%
%TAB%%TAB%position "renderActionsSLDR"
%TAB%%TAB%bam 'SLDRSTAR'
%TAB%%TAB%mosaic GUISLDR
%TAB%%TAB%sequence 0
%TAB%%TAB%frame 1
%TAB%%TAB%settings %ctl_num_settings%
%TAB%%TAB%pad %ctl_pad_x% %ctl_pad_y% %ctl_pad_w% %ctl_pad_h%
%TAB%%TAB%action
%TAB%%TAB%"
%TAB%%TAB%%TAB%helpString = '%ctl_description%'
%TAB%%TAB%%TAB%selectedOpt = 0
%TAB%%TAB%%TAB%selectedSL = %ctl_id%
%TAB%%TAB%"
%TAB%}
%TAB%label
%TAB%{
%TAB%%TAB%area %ctl_info_x% %ctl_info_y% %ctl_info_w% %ctl_info_h%
%TAB%%TAB%text lua "getRenderActions(renderActionsSLDR)"
%TAB%%TAB%text style "label"
%TAB%%TAB%text align center center
%TAB%}
%TAB%
~

/**
 * This code snippet adds a new action feedback slider control specifically to PST:EE.
 * The following variables are required to be set beforehand:
 * INT_VAR Label text position: ctl_label_x, ctl_label_y, ctl_label_w, ctl_label_h
 * INT_VAR Slider position: ctl_slider_x, ctl_slider_y, ctl_slider_w, ctl_slider_h
 * INT_VAR Slider info position: ctl_info_x, ctl_info_y, ctl_info_w, ctl_info_h
 * INT_VAR Control ID: ctl_id
 * INT_VAR Number of slider items: ctl_num_settings
 * STR_VAR LUA label text variable: ctl_label
 * STR_VAR LUA description strref: ctl_desc_strref
 * STR_VAR LUA description variable: ctl_description (UNUSED)
 */
OUTER_TEXT_SPRINT codeActionFeedbackSlider_PSTEE ~%TAB%
%TAB%text
%TAB%{
%TAB%%TAB%area %ctl_label_x% %ctl_label_y% %ctl_label_w% %ctl_label_h%
%TAB%%TAB%text "%ctl_label%"
%TAB%%TAB%text style "normal_parchment"
%TAB%%TAB%text useFontZoom 0
%TAB%%TAB%text align left center
%TAB%%TAB%focus color 255 255 255 255
%TAB%%TAB%text color lua "getLabelColor(%ctl_id%)"
%TAB%%TAB%action
%TAB%%TAB%"
%TAB%%TAB%%TAB%helpString = %ctl_desc_strref%
%TAB%%TAB%%TAB%selFeedOpt = 0
%TAB%%TAB%%TAB%selMessageOpt = 0
%TAB%%TAB%%TAB%selectedSL = %ctl_id%
%TAB%%TAB%"
%TAB%}
%TAB%slider
%TAB%{
%TAB%%TAB%area %ctl_slider_x% %ctl_slider_y% %ctl_slider_w% %ctl_slider_h%
%TAB%%TAB%position "renderActionsSLDR"
%TAB%%TAB%bam 'SLDRSTAR'
%TAB%%TAB%mosaic sliderbg
%TAB%%TAB%settings %ctl_num_settings%
%TAB%%TAB%action
%TAB%%TAB%"
%TAB%%TAB%%TAB%helpString = %ctl_desc_strref%
%TAB%%TAB%%TAB%selFeedOpt = 0
%TAB%%TAB%%TAB%selMessageOpt = 0
%TAB%%TAB%%TAB%selectedSL = %ctl_id%
%TAB%%TAB%"
%TAB%}
%TAB%label
%TAB%{
%TAB%%TAB%area %ctl_info_x% %ctl_info_y% %ctl_info_w% %ctl_info_h%
%TAB%%TAB%text lua "getRenderActions(renderActionsSLDR)"
%TAB%%TAB%text style "normal"
%TAB%%TAB%text useFontZoom 0
%TAB%%TAB%text align center center
%TAB%}
%TAB%
~

/**
 * Adds an action feedback slider to the Gameplay > Feedback section of the game.
 * INT_VAR optLabelTra    The TRA reference number of the option label string.
 * INT_VAR optDescTra     The TRA reference number of the option description text.
 * INT_VAR optInfo_0, ... Zero, one or more additional TRA reference numbers for lua text entries.
 * INT_VAR controlID      A unique number that identifies this option.
 * INT_VAR numSettings    Number of available slider settings.
 * INT_VAR autoFail       Set to nonzero to force termination of component installation on error.
 * STR_VAR optLabelName   Name of the option label variable.
 * STR_VAR optDescName    Name of the option description variable.
 * STR_VAR optInfoName_0  Zero, one or more additional strings used by the control. Number of entries 
 *                        must match the number of optInfo parameters.
 * STR_VAR iniSection     Section name of the option that will be added to baldur.lua (e.g. "Program Options").
 * STR_VAR iniName        Option name that will be added to baldur.lua (e.g. "Debug Mode").
 * RET success            Returns 1 on success, 0 otherwise.
 */
DEFINE_ACTION_FUNCTION INSTALL_SLIDER_ACTION_FEEDBACK
INT_VAR
  optLabelTra   = "-1"
  optDescTra    = "-1"
  // optInfo_0     = "-1"
  controlID     = 0
  numSettings   = 5
  autoFail      = 1
STR_VAR
  optLabelName  = ""
  optDescName   = ""
  // optInfoName_0 = ""
  iniSection    = ""
  iniName       = ""
RET
  success
BEGIN
  OUTER_SET success = 0

  ACTION_IF (optLabelTra >= 0 AND
             optDescTra >= 0 AND
             controlID != 0 AND
             numSettings > 0 AND
             NOT ~%optLabelName%~ STR_EQ ~~ AND
             NOT ~%optDescName%~ STR_EQ ~~ AND
             NOT ~%iniSection%~ STR_EQ ~~ AND
             NOT ~%iniName%~ STR_EQ ~~) BEGIN
    OUTER_SET failed = 0

    ACTION_IF (NOT GAME_IS ~pstee~) BEGIN
      LAF ADD_LUA_DEFINITION
      INT_VAR traRef = optDescTra
      STR_VAR varName = EVAL ~%optDescName%~
      RET success
      END
      ACTION_IF (NOT success) BEGIN
        ACTION_IF (autoFail) BEGIN
          FAIL @101 // Cannot patch LUA file
        END ELSE BEGIN
          OUTER_SET failed = 1
        END
      END
    END

    ACTION_IF (NOT failed) BEGIN
      LAF ADD_LUA_DEFINITION
      INT_VAR traRef = optLabelTra
      STR_VAR varName = EVAL ~%optLabelName%~
      RET success
      END
      ACTION_IF (NOT success) BEGIN
        ACTION_IF (autoFail) BEGIN
          FAIL @101 // Cannot patch LUA file
        END ELSE BEGIN
          OUTER_SET failed = 1
        END
      END
    END

    ACTION_IF (NOT failed) BEGIN
      OUTER_SET num = 0
      OUTER_WHILE (num != "-1") BEGIN
        ACTION_IF (VARIABLE_IS_SET EVAL ~optInfo_%num%~ AND VARIABLE_IS_SET EVAL ~optInfoName_%num%~) BEGIN
          OUTER_SET traRef = EVAL ~optInfo_%num%~
          OUTER_TEXT_SPRINT varName EVAL ~%optInfoName_%num%%~
          ACTION_IF (traRef >= 0 AND NOT ~%varName%~ STR_EQ ~~) BEGIN
            LAF ADD_LUA_DEFINITION
            INT_VAR traRef
            STR_VAR varName
            RET success
            END
            ACTION_IF (NOT success) BEGIN
              ACTION_IF (autoFail) BEGIN
                FAIL @101 // Cannot patch LUA file
              END ELSE BEGIN
                OUTER_SET failed = 1
              END
            END
          END
          OUTER_SET num += 1
        END ELSE BEGIN
          OUTER_SET num = "-1"
        END
      END
    END

    ACTION_IF (NOT failed) BEGIN
      COPY_EXISTING ~ui.menu~ ~override~
        LPF ADD_SLIDER_ACTION_FEEDBACK
        INT_VAR
          controlID
          numSettings
          optDescTra
        STR_VAR
          optLabel    = EVAL ~%optLabelName%~
          optDesc     = EVAL ~%optDescName%~
          iniSection  = EVAL ~%iniSection%~
          iniName     = EVAL ~%iniName%~
        RET
          success
        END
        PATCH_IF (NOT success) BEGIN
          PATCH_IF (autoFail) BEGIN
            PATCH_FAIL @102 // Cannot patch UI.MENU
          END ELSE BEGIN
            SET failed = 1
          END
        END
    END

    ACTION_IF (NOT failed AND GAME_IS ~bgee~ AND NOT GAME_INCLUDES ~sod~) BEGIN
      // Installing updated MOS background
      COPY ~%MOD_FOLDER%/mos/A7_UIGPF.MOS~ ~override~
        LPF UPDATE_PVRZ_INDICES RET original_base_index new_base_index END

      ACTION_IF (original_base_index >= 0 AND new_base_index >= 0) BEGIN
        LAF INSTALL_PVRZ
        INT_VAR
          original_base_index
          new_base_index
        STR_VAR
          source_file = EVAL ~%MOD_FOLDER%/mos/MOS1001.PVRZ~
        END
      END
    END

    OUTER_SET success = failed ? 0 : 1
  END
END


/**
 * Adds an action feedback slider to the Gameplay > Feedback section of the Options menu.
 * INT_VAR controlID          A unique number that identifies the slider control.
 * INT_VAR numSettings        Number of available slider settings.
 * INT_VAR optDescTra         The TRA reference number of the option description text. (Needed by PST:EE.)
 * STR_VAR optLabel           Variable name referring to option label text.
 * STR_VAR optDesc            Variable name referring to option description text.
 * STR_VAR optInfoName_0, ... Zero, one or more variable names referring to descriptive strings used for individual slider positions.
 * STR_VAR iniSection         Section name of option that will be added to baldur.lua (e.g. "Program Options").
 * STR_VAR iniName            Option name that will be added to baldur.lua (e.g. "Debug Mode").
 * RET success                Returns 1 if changes have been applied successfully, 0 otherwise.
 */
DEFINE_PATCH_FUNCTION ADD_SLIDER_ACTION_FEEDBACK
INT_VAR
  controlID   = controlID
  numSettings = 5
  optDescTra  = "-1"
STR_VAR
  optLabel    = ""
  optDesc     = ""
  iniSection  = ""
  iniName     = ""
RET
  success
BEGIN
  SET success = 0
  PATCH_IF (controlID != 0 AND
            numSettings > 0 AND
            NOT ~%optLabel%~ STR_EQ ~~ AND
            NOT ~%optDesc%~ STR_EQ ~~ AND
            NOT ~%iniSection%~ STR_EQ ~~ AND
            NOT ~%iniName%~ STR_EQ ~~) BEGIN
    LPF __A7_INIT_CODE RET success END
    PATCH_IF (success) BEGIN
      SET upperCase = GAME_IS ~pstee~ ? 0 : 1
      LPF __A7_INIT_SLIDER_ACTION_FEEDBACK
      INT_VAR
        numSettings
        upperCase
      STR_VAR
        iniSection
        iniName
      RET
        success
      END
      PATCH_IF (success) BEGIN
        PATCH_IF (GAME_IS ~bgee~) BEGIN
          PATCH_IF (GAME_INCLUDES ~sod~) BEGIN
            // BGEE+SoD
            LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_BGSOD RET success END
          END ELSE BEGIN
            // Pure BG1EE
            LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_BGEE RET success END
          END
        END ELSE PATCH_IF (GAME_IS ~iwdee~) BEGIN
          // IWDEE
          LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_IWDEE RET success END
        END ELSE PATCH_IF (GAME_IS ~pstee~) BEGIN
          // PSTEE
          LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_PSTEE INT_VAR optDescTra RET success END
        END ELSE PATCH_IF (GAME_IS ~eet~ && MOD_IS_INSTALLED ~EET_gui.tp2~ 0) BEGIN
          // EET with SoD GUI
          LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_BGSOD RET success END
        END ELSE BEGIN
          // BG2EE or EET
          LPF __A7_ADD_SLIDER_ACTION_FEEDBACK_BG2EE RET success END
        END
      END
    END
  END
END


DEFINE_PATCH_FUNCTION __A7_INIT_SLIDER_ACTION_FEEDBACK
INT_VAR
  numSettings = 5
  upperCase   = 1
STR_VAR
  iniSection  = ""
  iniName     = ""
RET
  success
BEGIN
  SET success = 0

  // implements common functionality
  // renderActionsSLDR = 0
  SET ofs = INDEX_BUFFER(~locatorFeedSLDR[ %TAB%]*=[ %TAB%]*0~)
  PATCH_IF (ofs > 0) BEGIN
    SET ofs = INDEX_BUFFER(~[%WNL%]~ ofs)
    PATCH_IF (ofs > 0) BEGIN
      TEXT_SPRINT line ~%WNL%renderActionsSLDR = 0~
      SET strlen = STRING_LENGTH ~%line%~
      INSERT_BYTES ofs strlen
      WRITE_ASCIIE ofs ~%line%~ (strlen)
    END
  END

  // function getRenderActions(l) ...
  PATCH_IF (ofs > 0) BEGIN
    PATCH_IF (upperCase) BEGIN
      TEXT_SPRINT fn_upperOpen ~string.upper(~
      TEXT_SPRINT fn_upperClose ~)~
    END ELSE BEGIN
      TEXT_SPRINT fn_upperOpen ~~
      TEXT_SPRINT fn_upperClose ~~
    END
    TEXT_SPRINT code ~function getRenderActions(l)%WNL%  local text = ""%WNL%~
    SET num = 0
    WHILE (num != "-1") BEGIN
      PATCH_IF (VARIABLE_IS_SET EVAL ~optInfoName_%num%~) BEGIN
        PATCH_IF (num = 0) BEGIN TEXT_SPRINT if ~if~ END ELSE BEGIN TEXT_SPRINT if ~elseif~ END
        TEXT_SPRINT label EVAL ~%optInfoName_%num%%~
        TEXT_SPRINT code ~%code%  %if% (l == %num%) then%WNL%    text = %fn_upperOpen%t("%label%")%fn_upperClose%%WNL%~
        SET num += 1
      END ELSE BEGIN
        PATCH_IF (num > 0) BEGIN
          TEXT_SPRINT code ~%code%  end%WNL%~
        END
        SET num = "-1"
      END
    END
    TEXT_SPRINT code ~%code%  return text%WNL%end%WNL%~
    SET ofs = INDEX_BUFFER(~^[ %TAB%]*`~ ofs)
    PATCH_IF (ofs > 0) BEGIN
      SET strlen = STRING_LENGTH ~%code%~
      INSERT_BYTES ofs strlen
      WRITE_ASCIIE ofs ~%code%~ (strlen)
    END
  END

  // renderActionsSLDR = math.min(math.max(getDeferredOption('Game Options', 'Render Actions'), 0), max)
  PATCH_IF (ofs > 0) BEGIN
    SET ofs = INDEX_BUFFER(~locatorFeedSLDR[ %TAB%]*=[ %TAB%]*Infinity_GetOption~)
    PATCH_IF (ofs > 0) BEGIN
      SET ofs = INDEX_BUFFER(~[%WNL%]~ ofs)
      PATCH_IF (ofs > 0) BEGIN
        SET max = numSettings - 1
        TEXT_SPRINT line ~%WNL%%TAB%%TAB%renderActionsSLDR = math.min(math.max(getDeferredOption('%iniSection%', '%iniName%'), 0), %max%)~
        SET strlen = STRING_LENGTH ~%line%~
        INSERT_BYTES ofs strlen
        WRITE_ASCIIE ofs ~%line%~ (strlen)
      END
    END
  END

  // setDeferredOption('Game Options', 'Render Actions', renderActionsSLDR)
  PATCH_IF (ofs > 0) BEGIN
    SET ofs = INDEX_BUFFER(~Infinity_ChangeOption([ %TAB%]*9,[ %TAB%]*locatorFeedSLDR.+)~)
    PATCH_IF (ofs > 0) BEGIN
      SET ofs = INDEX_BUFFER(~[%WNL%]~ ofs)
      PATCH_IF (ofs > 0) BEGIN
        TEXT_SPRINT line ~%WNL%%TAB%%TAB%%TAB%setDeferredOption('%iniSection%', '%iniName%', renderActionsSLDR)~
        SET strlen = STRING_LENGTH ~%line%~
        INSERT_BYTES ofs strlen
        WRITE_ASCIIE ofs ~%line%~ (strlen)
      END
    END
  END

  SET success = (ofs > 0) ? 1 : 0
END


DEFINE_PATCH_FUNCTION __A7_ADD_SLIDER_ACTION_FEEDBACK_BGEE
RET
  success
BEGIN
  SET success = 1

  SET ofsStart = INDEX_BUFFER(~name[ %TAB%]+['"]OPTIONS_FEEDBACK['"]~)
  PATCH_IF (ofsStart > 0) BEGIN
    SET ofsEnd = INDEX_BUFFER(~^[ %TAB%]*menu[ %TAB%%WNL%]*{~ ofsStart)
    PATCH_IF (ofsEnd > ofsStart) BEGIN
      // adjusting screen background
      SET ofs1 = INDEX_BUFFER(~area[ %TAB%]+0[ %TAB%]+~ ofsStart)
      PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
        SET ofs1 = INDEX_BUFFER(~mosaic[ %TAB%]+.+~ ofs1)
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          SET ofs2 = INDEX_BUFFER(~[%WNL%]~ ofs1)
          PATCH_IF (ofs2 > ofs1) BEGIN
            READ_ASCII ofs1 line (ofs2 - ofs1)
            DELETE_BYTES ofs1 (ofs2 - ofs1)
            SET ofsEnd -= ofs2 - ofs1
            TEXT_SPRINT line ~mosaic "A7_UIGPF"~
            SET strlen = STRING_LENGTH ~%line%~
            INSERT_BYTES ofs1 strlen
            WRITE_ASCIIE ofs1 ~%line%~
            SET ofsEnd += strlen
          END
        END
      END

      // adjusting info box dimension
      SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*area[ %TAB%]+446[ %TAB%]+422[ %TAB%]+372[ %TAB%]+184~ ofsStart)
      PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
        SET ofs2 = INDEX_BUFFER(~[%WNL%]~ ofs1)
        PATCH_IF (ofs2 > ofs1) BEGIN
          READ_ASCII ofs1 line (ofs2 - ofs1)
          INNER_PATCH_SAVE line ~%line%~ BEGIN
            REPLACE_TEXTUALLY ~422~ ~452~
            REPLACE_TEXTUALLY ~184~ ~154~
          END
          WRITE_ASCIIE ofs1 ~%line%~ (ofs2 - ofs1)
        END
      END ELSE BEGIN
        SET success = 0
      END

      // adjusting vertical position of existing sliders
      LPF __A7_UPDATE_SLIDER_POSITIONS
      INT_VAR
        ofsStart
        gap       = 75
      STR_VAR
        keyword_0 = ~MARKER_FEEDBACK_LABEL~
        keyword_1 = ~LOCATOR_FEEDBACK_LABEL~
        keyword_2 = ~CANCEL_BUTTON~
      RET
        success
      END

      // adding action feedback slider code
      PATCH_IF (success) BEGIN
        SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*slider[ %TAB%%WNL%]*{~ ofsStart)
        PATCH_IF (ofs1 > ofsStart) BEGIN
          SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*button[ %TAB%%WNL%]*{~ ofs1)
        END
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          SET ctl_label_x  = 446  SET ctl_label_y  = 348  SET ctl_label_w  = 172  SET ctl_label_h  = 36
          SET ctl_slider_x = 622  SET ctl_slider_y = 348  SET ctl_slider_w = 200  SET ctl_slider_h = 36
          SET ctl_info_x   = 622  SET ctl_info_y   = 384  SET ctl_info_w   = 200  SET ctl_info_h   = 36
          SET ctl_id = controlID
          SET ctl_num_settings = numSettings
          TEXT_SPRINT ctl_label ~%optLabelName%~
          TEXT_SPRINT ctl_description ~%optDescName%~

          TEXT_SPRINT code EVAL ~%codeActionFeedbackSlider%~
          INNER_PATCH_SAVE code ~%code%~ BEGIN
            // padding not needed
            REPLACE_TEXTUALLY ~^[ %TAB%]*pad[ %TAB%].*[%WNL%]*~ ~~
          END
          SET strlen = STRING_LENGTH ~%code%~
          INSERT_BYTES ofs1 strlen
          WRITE_ASCIIE ofs1 ~%code%~ (strlen)
        END ELSE BEGIN
          SET success = 0
        END
      END
    END ELSE BEGIN
      SET success = 0
    END
  END ELSE BEGIN
    SET success = 0
  END
END


DEFINE_PATCH_FUNCTION __A7_ADD_SLIDER_ACTION_FEEDBACK_BGSOD
RET
  success
BEGIN
  SET success = 1

  SET ofsStart = INDEX_BUFFER(~name[ %TAB%]+['"]OPTIONS_FEEDBACK['"]~)
  PATCH_IF (ofsStart > 0) BEGIN
    SET ofsEnd = INDEX_BUFFER(~^[ %TAB%]*menu[ %TAB%%WNL%]*{~ ofsStart)
    PATCH_IF (ofsEnd > ofsStart) BEGIN
      // adjusting vertical position of existing sliders
      SET gap = (MOD_IS_INSTALLED ~dragonspear_ui++.tp2~ 1) ? 65 : 75
      LPF __A7_UPDATE_SLIDER_POSITIONS
      INT_VAR
        ofsStart
        gap
        adjust    = "-10"
      STR_VAR
        keyword_0 = ~MARKER_FEEDBACK_LABEL~
        keyword_1 = ~LOCATOR_FEEDBACK_LABEL~
        keyword_2 = ~CANCEL_BUTTON~
      RET
        success
      END

      // adding action feedback slider code
      PATCH_IF (success) BEGIN
        SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*slider[ %TAB%%WNL%]*{~ ofsStart)
        PATCH_IF (ofs1 > ofsStart) BEGIN
          SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*button[ %TAB%%WNL%]*{~ ofs1)
        END
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          PATCH_IF (MOD_IS_INSTALLED ~dragonspear_ui++.tp2~ 1) BEGIN
            SET ctl_label_x  = 110  SET ctl_label_y  = 262  SET ctl_label_w  = 186  SET ctl_label_h  = 36
            SET ctl_slider_x = 300  SET ctl_slider_y = 262  SET ctl_slider_w = 200  SET ctl_slider_h = 36
            SET ctl_info_x   = 300  SET ctl_info_y   = 298  SET ctl_info_w   = 200  SET ctl_info_h   = 36
          END ELSE BEGIN
            SET ctl_label_x  = 110  SET ctl_label_y  = 305  SET ctl_label_w  = 186  SET ctl_label_h  = 36
            SET ctl_slider_x = 300  SET ctl_slider_y = 305  SET ctl_slider_w = 200  SET ctl_slider_h = 36
            SET ctl_info_x   = 300  SET ctl_info_y   = 341  SET ctl_info_w   = 200  SET ctl_info_h   = 36
          END
          SET ctl_id = controlID
          SET ctl_num_settings = numSettings
          TEXT_SPRINT ctl_label ~%optLabelName%~
          TEXT_SPRINT ctl_description ~%optDescName%~

          TEXT_SPRINT code EVAL ~%codeActionFeedbackSlider%~
          INNER_PATCH_SAVE code ~%code%~ BEGIN
            // padding not needed
            REPLACE_TEXTUALLY ~^[ %TAB%]*pad[ %TAB%].*[%WNL%]*~ ~~
          END
          SET strlen = STRING_LENGTH ~%code%~
          INSERT_BYTES ofs1 strlen
          WRITE_ASCIIE ofs1 ~%code%~ (strlen)
        END ELSE BEGIN
          SET success = 0
        END
      END
    END ELSE BEGIN
      SET success = 0
    END
  END ELSE BEGIN
    SET success = 0
  END
END


DEFINE_PATCH_FUNCTION __A7_ADD_SLIDER_ACTION_FEEDBACK_BG2EE
RET
  success
BEGIN
  SET success = 1

  SET ofsStart = INDEX_BUFFER(~name[ %TAB%]+['"]OPTIONS_FEEDBACK['"]~)
  PATCH_IF (ofsStart > 0) BEGIN
    SET ofsEnd = INDEX_BUFFER(~^[ %TAB%]*menu[ %TAB%%WNL%]*{~ ofsStart)
    PATCH_IF (ofsEnd > ofsStart) BEGIN
      // adjusting vertical position of existing sliders
      LPF __A7_UPDATE_SLIDER_POSITIONS
      INT_VAR
        ofsStart
        gap       = 75
        adjust    = "-15"
      STR_VAR
        keyword_0 = ~MARKER_FEEDBACK_LABEL~
        keyword_1 = ~LOCATOR_FEEDBACK_LABEL~
        keyword_2 = ~CANCEL_BUTTON~
      RET
        success
      END

      // adding action feedback slider code
      PATCH_IF (success) BEGIN
        SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*slider[ %TAB%%WNL%]*{~ ofsStart)
        PATCH_IF (ofs1 > ofsStart) BEGIN
          SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*button[ %TAB%%WNL%]*{~ ofs1)
        END
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          SET ctl_label_x  = 106  SET ctl_label_y  = 300  SET ctl_label_w  = 190  SET ctl_label_h  = 36
          SET ctl_slider_x = 300  SET ctl_slider_y = 300  SET ctl_slider_w = 200  SET ctl_slider_h = 36
          SET ctl_info_x   = 300  SET ctl_info_y   = 336  SET ctl_info_w   = 200  SET ctl_info_h   = 36
          SET ctl_id = controlID
          SET ctl_num_settings = numSettings
          TEXT_SPRINT ctl_label ~%optLabelName%~
          TEXT_SPRINT ctl_description ~%optDescName%~

          TEXT_SPRINT code EVAL ~%codeActionFeedbackSlider%~
          INNER_PATCH_SAVE code ~%code%~ BEGIN
            // padding not needed
            REPLACE_TEXTUALLY ~^[ %TAB%]*pad[ %TAB%].*[%WNL%]*~ ~~
          END
          SET strlen = STRING_LENGTH ~%code%~
          INSERT_BYTES ofs1 strlen
          WRITE_ASCIIE ofs1 ~%code%~ (strlen)
        END ELSE BEGIN
          SET success = 0
        END
      END
    END ELSE BEGIN
      SET success = 0
    END
  END ELSE BEGIN
    SET success = 0
  END
END


DEFINE_PATCH_FUNCTION __A7_ADD_SLIDER_ACTION_FEEDBACK_IWDEE
RET
  success
BEGIN
  SET success = 1

  SET ofsStart = INDEX_BUFFER(~name[ %TAB%]+['"]OPTIONS_FEEDBACK['"]~)
  PATCH_IF (ofsStart > 0) BEGIN
    SET ofsEnd = INDEX_BUFFER(~^[ %TAB%]*menu[ %TAB%%WNL%]*{~ ofsStart)
    PATCH_IF (ofsEnd > ofsStart) BEGIN
      // adjusting vertical position of existing sliders
      LPF __A7_UPDATE_SLIDER_POSITIONS
      INT_VAR
        ofsStart
        gap       = 75
        adjust    = "-15"
      STR_VAR
        keyword_0 = ~MARKER_FEEDBACK_LABEL~
        keyword_1 = ~LOCATOR_FEEDBACK_LABEL~
        keyword_2 = ~CANCEL_BUTTON~
      RET
        success
      END

      // adding action feedback slider code
      PATCH_IF (success) BEGIN
        SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*slider[ %TAB%%WNL%]*{~ ofsStart)
        PATCH_IF (ofs1 > ofsStart) BEGIN
          SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*button[ %TAB%%WNL%]*{~ ofs1)
        END
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          SET ctl_label_x  = 106  SET ctl_label_y  = 300  SET ctl_label_w  = 132  SET ctl_label_h  = 36
          SET ctl_slider_x = 246  SET ctl_slider_y = 300  SET ctl_slider_w = 254  SET ctl_slider_h = 36
          SET ctl_info_x   = 246  SET ctl_info_y   = 336  SET ctl_info_w   = 254  SET ctl_info_h   = 36
          SET ctl_pad_x    = 8    SET ctl_pad_y    = 0    SET ctl_pad_w    = 10   SET ctl_pad_h    = 0
          SET ctl_id = controlID
          SET ctl_num_settings = numSettings
          TEXT_SPRINT ctl_label ~%optLabelName%~
          TEXT_SPRINT ctl_description ~%optDescName%~

          TEXT_SPRINT code EVAL ~%codeActionFeedbackSlider%~
          SET strlen = STRING_LENGTH ~%code%~
          INSERT_BYTES ofs1 strlen
          WRITE_ASCIIE ofs1 ~%code%~ (strlen)
        END ELSE BEGIN
          SET success = 0
        END
      END
    END ELSE BEGIN
      SET success = 0
    END
  END ELSE BEGIN
    SET success = 0
  END
END


DEFINE_PATCH_FUNCTION __A7_ADD_SLIDER_ACTION_FEEDBACK_PSTEE
INT_VAR
  optDescTra = "-1"
RET
  success
BEGIN
  SET success = 1

  SET ofsStart = INDEX_BUFFER(~name[ %TAB%]+['"]OPTIONS_FEEDBACK['"]~)
  PATCH_IF (ofsStart > 0) BEGIN
    SET ofsEnd = INDEX_BUFFER(~^[ %TAB%]*menu[ %TAB%%WNL%]*{~ ofsStart)
    PATCH_IF (ofsEnd > ofsStart) BEGIN
      // adding action feedback slider code
      PATCH_IF (success) BEGIN
        SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*slider[ %TAB%%WNL%]*{~ ofsStart)
        PATCH_IF (ofs1 > ofsStart) BEGIN
          SET ofs1 = INDEX_BUFFER(~^[ %TAB%]*button[ %TAB%%WNL%]*{~ ofs1)
        END
        PATCH_IF (ofs1 > ofsStart && ofs1 < ofsEnd) BEGIN
          SET ctl_label_x  = 111  SET ctl_label_y  = 476  SET ctl_label_w  = 192  SET ctl_label_h  = 36
          SET ctl_slider_x = 318  SET ctl_slider_y = 479  SET ctl_slider_w = 105  SET ctl_slider_h = 27
          SET ctl_info_x   = 318  SET ctl_info_y   = 511  SET ctl_info_w   = 104  SET ctl_info_h   = 26
          SET ctl_id = controlID
          SET ctl_num_settings = numSettings
          TEXT_SPRINT ctl_label ~%optLabelName%~
          SPRINT ctl_description (AT optDescTra)
          SET ctl_desc_strref = RESOLVE_STR_REF(~%ctl_description%~)

          TEXT_SPRINT code EVAL ~%codeActionFeedbackSlider_PSTEE%~
          SET strlen = STRING_LENGTH ~%code%~
          INSERT_BYTES ofs1 strlen
          WRITE_ASCIIE ofs1 ~%code%~ (strlen)
        END ELSE BEGIN
          SET success = 0
        END
      END
    END ELSE BEGIN
      SET success = 0
    END
  END ELSE BEGIN
    SET success = 0
  END
END
